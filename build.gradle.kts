plugins {
    java
    alias(libs.plugins.runvelocity)
    alias(libs.plugins.blossom)
    alias(libs.plugins.shadow)
}

repositories {
    maven("https://repo.papermc.io/repository/maven-public/")
    maven("https://mvn.exceptionflug.de/repository/exceptionflug-public/")
}

dependencies {
    compileOnly(libs.velocity.api)
    annotationProcessor(libs.velocity.api)
    compileOnly(libs.velocity.proxy)
    compileOnly(libs.netty)
    compileOnly(libs.miniplaceholders)
    implementation(libs.velocityhexlogger)
}

tasks {
    build {
        dependsOn(shadowJar)
    }
    compileJava {
        options.encoding = Charsets.UTF_8.name()
        options.release.set(17)
    }
    clean {
        // Deletes the directory that is generated by the runVelocity plugin
        delete("run")
    }
    runVelocity {
        velocityVersion(libs.versions.velocity.get())
    }
    shadowJar {
        archiveBaseName.set(rootProject.name)
        archiveClassifier.set("")
        relocate("io.github._4drian3d.velocityhexlogger", "io.github._4drian3d.vcustombrand.velocityhexlogger")
        relocate("net.kyori.adventure.text.logger.slf4j", "io.github._4drian3d.vcustombrand.component.logger")
        minimize()
    }
}

blossom {
    replaceTokenIn("src/main/java/io/github/_4drian3d/vcustombrand/Constants.java")
    replaceToken("{version}", project.version)
}

java.toolchain.languageVersion.set(JavaLanguageVersion.of(17))
